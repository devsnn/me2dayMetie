<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow creationComplete="create()" backgroundColor="#666666" initialize="init()" xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="100%" height="100%">
	<mx:Script>
		<![CDATA[
			import flash.display.Sprite;
			import flash.events.Event;
			
			import header.DBConnecter;
			
			import mx.controls.Alert;
			import mx.controls.Button;
			import mx.events.CloseEvent;
			import mx.rpc.events.ResultEvent;
			
			import request.RequestAuth;
			import request.RequestAuthToken;
			
			import util.UserSetting;
			
			
			private var token:String;
			[Bindable] private var msg:String = ""; 
			
			private function init():void{
				
			}
			
			private function create():void{
				Alert.show("미투데이 사이트에서 수락을 눌러야 이용이 가능합니다. 확인을 누르면 미투데이 사이트로 이동합니다.", "인증", Alert.OK+Alert.CANCEL , this.parent as Sprite, close_AlertHandler);
			}
			
			
			private function close_AlertHandler(e:CloseEvent):void{
				if(Alert.OK == e.detail){
					request();
				}
			}
			
			
			private function request():void{
				var req:RequestAuth = new RequestAuth();
				var dbConnect:DBConnecter = new DBConnecter();
				dbConnect.addEventListener(ResultEvent.RESULT, result_authHandler);
				dbConnect.query(req);
			}
			
			
			private function result_authHandler(e:ResultEvent):void{
				navigateToURL(new URLRequest(e.result.url), "_blank");
				
				token = e.result.token;
			}
			
			
			private function requestAuthToken(e:Event):void{
				(e.target as Button).enabled = false;
				
				var req:RequestAuthToken = new RequestAuthToken(token);
				var dbConnect:DBConnecter = new DBConnecter();
				dbConnect.addEventListener(ResultEvent.RESULT, result_authTokenHandler);
				dbConnect.query(req);
			}
			
			private var _user_id:String;
			private var _full_auth_token:String;
			
			private function result_authTokenHandler(e:ResultEvent):void{
				Alert.show("로그인정보를 유지하겠습니까? 공공장소에서는 사용하지마세요!", "알림", Alert.YES + Alert.NO, this.parent as Sprite, auth_closeHandler);
				msg = "인증이 완료되었습니다!";
				
				_user_id = e.result.user_id;
				_full_auth_token = e.result.full_auth_token;
			}
			
			
			private function auth_closeHandler(e:CloseEvent):void{
				UserSetting.user_id = _user_id;
				UserSetting.full_auth_token = _full_auth_token;
				
				// 인증정보를 유지 
				if(e.detail == Alert.YES){
					var so:SharedObject = SharedObject.getLocal("metie");
					so.data.user_id = new String(_user_id);
					so.data.full_auth_token = new String(_full_auth_token);
					so.flush(10000);
				}
				
				this.dispatchEvent(new Event(Event.COMPLETE));
			}
			
			
		]]>
	</mx:Script>
	<mx:Label text="{msg}" horizontalCenter="0" verticalCenter="0"/>
	<mx:VBox width="100%" horizontalAlign="center" horizontalCenter="0" verticalCenter="28">
		<mx:Button label="사이트에서 수락을 눌렀습니다!" color="#000000" height="40" click="requestAuthToken(event)"/>	
		<mx:Button label="인증 다시요청" color="#000000" click="request()"/>
	</mx:VBox>
	
	
</mx:TitleWindow>
