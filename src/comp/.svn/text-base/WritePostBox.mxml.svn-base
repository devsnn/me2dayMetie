<?xml version="1.0" encoding="utf-8"?>
<comp:GradientHBox creationComplete="create()" xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:comp="comp.*" width="100%" height="132" verticalScrollPolicy="off" borderStyle="solid" borderColor="#999999" filters="{[shadow]}" paddingBottom="4" paddingLeft="4" paddingRight="4" paddingTop="4">
	<mx:Script>
		<![CDATA[
			import com.lhd.MeConnect;
			import com.lhd.events.MeConnectEvent;
			
			import flash.events.Event;
			import flash.events.FocusEvent;
			import flash.events.KeyboardEvent;
			import flash.events.MouseEvent;
			import flash.events.TimerEvent;
			import flash.net.FileFilter;
			import flash.net.FileReference;
			import flash.net.SharedObject;
			import flash.utils.Timer;
			
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			
			
			[Bindable] private var text_count:String = "150";
			private var isUpload:Boolean = false;
			
			private function create():void{
				var so:SharedObject = SharedObject.getLocal("metie");
				if(so.data.tagText != null){
					tagInput.text = so.data.tagText;
				}		
				
				textArea.addEventListener(MouseEvent.CLICK, textArea_clickHandler);
				textArea.addEventListener(KeyboardEvent.KEY_UP, textArea_keyUpHandler);
				textArea.addEventListener(KeyboardEvent.KEY_DOWN, textArea_keyDownHandler);
				
				var timer:Timer = new Timer(1000);
				timer.addEventListener(TimerEvent.TIMER, timer_focusHandler);
				timer.start();
				
			}
			
			
			private function timer_focusHandler(e:TimerEvent):void{
				if(textArea.focusEnabled){
					messageTextLengthCheck();
				}
			}
			
			private function createPost():void{
				if(textArea.text.length > 0){
					writeBtn.enabled = false;
					var connect:MeConnect = new MeConnect();
					connect.addEventListener(MeConnectEvent.CREATEPOST_RESULT, result_createPostHandler);
					iconList.selectedIndex = iconList.verticalScrollPosition;
					connect.createpost(textArea.text, "metie " + tagInput.text, iconList.selectedItem.iconIndex, 1, 'document', null, null, false, 0, 0, fileR);
				}
			}
			
			
			private function result_createPostHandler(e:MeConnectEvent):void{
				this.dispatchEvent(new Event(Event.COMPLETE));
				textArea.text = "";
				
				var so:SharedObject = SharedObject.getLocal("metie");
				if(so.data.tagText){
					tagInput.text = so.data.tagText;
				}
				else{
					tagInput.text = "";
				}
				
				writeBtn.enabled = true;
			}
			
			
			protected function textArea_clickHandler(event:MouseEvent):void
			{
				textArea.removeEventListener(MouseEvent.CLICK, textArea_clickHandler);
				textArea.text = ""; 
			}
			
			
			protected function textArea_keyDownHandler(event:KeyboardEvent):void
			{
				if(event.keyCode == 13){
					createPost();
				}
				messageTextLengthCheck();
			}
			
			private function textArea_keyUpHandler(e:KeyboardEvent):void{
				messageTextLengthCheck();
			}
			
			
			public function messageTextLengthCheck():void{
				//var textCount:uint = getByteLength(textArea.text);
				//text_count = textCount + " / 150"; 
				text_count = (150 - textArea.text.length).toString();
			}
			
			public static function getByteLength(str:String):uint{
				var txtByte:Number = 0;
				for(var i:int=0; i < str.length; i++){
					var tmp:Number = str.charCodeAt(i);
					if(tmp > 256){
						txtByte += 2;
					}
					else{
						txtByte++;
					}
				}
				return txtByte;
			}
			

			protected function tagInput_focusInHandler(event:FocusEvent):void
			{
				tagInput.selectionBeginIndex = tagInput.text.length;
				tagInput.selectionEndIndex = tagInput.text.length;
			}
			
			
			// 링크걸기 
			private function link_clickHandler():void{
				if(textArea.text != "" && textArea.selectionBeginIndex != textArea.selectionEndIndex){
					var linkPanel:TextLinkPanel = new TextLinkPanel();
					PopUpManager.addPopUp(linkPanel, this, true);
					PopUpManager.centerPopUp(linkPanel);
					linkPanel.addEventListener(Event.COMPLETE, link_completeHandler);
				}
			}
			
			
			private function link_completeHandler(e:Event):void{
				var linkPanel:TextLinkPanel = e.target as TextLinkPanel;
				
				var first_text:String = textArea.text.substring(0, textArea.selectionBeginIndex);
				var end_text:String = textArea.text.substr(textArea.selectionEndIndex, textArea.text.length);
				
				var link_text:String = textArea.text.substring(textArea.selectionBeginIndex, textArea.selectionEndIndex);
				link_text = '"' + link_text + '":' + linkPanel.link_url+" ";
				
				textArea.text = first_text + link_text + end_text;
				
				PopUpManager.removePopUp(linkPanel);
				
			}
			
			
			private var fileR:FileReference;
			
			private function upload_clickHandler():void{
				fileR = new FileReference();
				fileR.browse([new FileFilter("Images (*.jpg, *.jpeg)", "*.jpg;*.jpeg;")]);
				fileR.addEventListener(Event.SELECT, file_selectHandler);
			}
			
			
			private function file_selectHandler(e:Event):void{
				Alert.show("파일이 첨부되었습니다.", "알림", 4, this);
				isUpload = true;
				
			}
			
			
		]]>
	</mx:Script>
	<mx:DropShadowFilter id="shadow" distance="2" alpha="0.3" blurX="5" blurY="5" color="#666666" strength="1"/>
	<mx:VBox width="100%" verticalGap="4" paddingBottom="2" paddingLeft="2" paddingRight="2" paddingTop="2">
		<mx:HBox width="100%">
			<comp:PersonIconTileList id="iconList" width="66" height="50"/>
			<comp:NovTextArea id="textArea" text="지금 내 생각을 작성합니다." keyUp="textArea_keyUpHandler(event)" keyDown="textArea_keyDownHandler(event)" width="100%" height="64" paddingBottom="2" paddingLeft="2" paddingRight="2" paddingTop="2"/>
		</mx:HBox>
		<mx:HBox width="100%">
			<comp:NovTextInput id="tagInput" focusIn="tagInput_focusInHandler(event)" width="100%" text="" keyDown="textArea_keyDownHandler(event)"/>
			<mx:Label id="textCountLb" text="{text_count}" fontSize="13" fontWeight="bold" width="40" color="#cccccc"/>
		</mx:HBox>	
		<mx:HBox width="100%" horizontalGap="2">
			<mx:LinkButton label="링크걸기" click="link_clickHandler()" styleName="writeBoxText"/>
			<mx:LinkButton label="사진첨부" click="upload_clickHandler()" styleName="writeBoxText"/>
		</mx:HBox>
	</mx:VBox>
	<mx:Button id="writeBtn" label="작성" click="createPost()" width="60" height="100%"/>
</comp:GradientHBox>
